# Maintainer: Matt Monaco <net 0x01b matt>

pkgname=vim-gist
pkgver=7.1.27.gfba218c
pkgrel=1
pkgdesc="vimscript for gist"
arch=(any)
url=https://github.com/mattn/gist-vim
license=(custom)
depends=(vim curl vim-webapi)
optdepends=("git: store settings in git config")
source=(git://github.com/mattn/gist-vim.git)
md5sums=(SKIP)
install=vim-helptags.install

pkgver()
{
	cd "$srcdir"/gist-vim
	# hopefully this gets tagged upstream
	git tag 7.1 28c34862d5d1fb6013c78046f95493b309ec94f0
	local ver=$(git describe --tags )
	printf "%s" "${ver//-/.}"
}

package()
{
	cd "$srcdir"/gist-vim
	for f in autoload/* doc/* plugin/*; do
		install -D "$f" "$pkgdir"/usr/share/vim/vimfiles/"$f"
	done
	install -D README.mkd "$pkgdir"/usr/share/licenses/"$pkgname"/README.mkd
}
